apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt' // TODO Migrate to KSP when stable

android {
    compileSdkVersion = 30

    defaultConfig {
        applicationId "dev.sjaramillo.pedometer"
        targetSdkVersion 30 // Check https://github.com/sjaramillo10/Pedometer/pull/14 before increasing version
        minSdkVersion 21
        versionCode 1
        versionName "0.1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        release
    }

    lintOptions{
        disable 'MissingTranslation'
        disable 'ExtraTranslation'
    }

    compileOptions {
        // Flag to enable support for the new language APIs
        coreLibraryDesugaringEnabled = true
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
            zipAlignEnabled true
            minifyEnabled true
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            debuggable true
            signingConfig signingConfigs.release
            zipAlignEnabled true
            minifyEnabled false
        }
    }
}
dependencies {
    // Enable Java 8+ API desugaring
    coreLibraryDesugaring("com.android.tools:desugar_jdk_libs:1.1.5")

    def nav_version = "2.3.5"
    def roomVersion = "2.3.0"
    def work_version = "2.6.0" // Check https://github.com/sjaramillo10/Pedometer/pull/14 before increasing version

    // AndroidX
    implementation 'androidx.appcompat:appcompat:1.3.1'
    implementation 'androidx.activity:activity-ktx:1.3.1'
    implementation 'androidx.fragment:fragment-ktx:1.3.6'
    implementation 'androidx.preference:preference-ktx:1.1.1'

    // Navigation
    implementation "androidx.navigation:navigation-fragment-ktx:$nav_version"
    implementation "androidx.navigation:navigation-ui-ktx:$nav_version"

    // Material Components
    implementation 'com.google.android.material:material:1.4.0'

    // Room
    implementation "androidx.room:room-runtime:$roomVersion"
    implementation "androidx.room:room-ktx:$roomVersion"
    kapt "androidx.room:room-compiler:$roomVersion"

    // WorkManager
    implementation "androidx.work:work-runtime-ktx:$work_version"

    // Other
    implementation 'com.squareup.logcat:logcat:0.1'
    implementation 'com.github.j4velin.EazeGraph:EazeGraph:1.0.3'

    // Testing
    testImplementation "junit:junit:4.13.2"
    androidTestImplementation "androidx.arch.core:core-testing:2.1.0"
    androidTestImplementation "androidx.test.ext:junit:1.1.3"
    androidTestImplementation "androidx.test:rules:1.4.0"
    androidTestImplementation "androidx.test:runner:1.4.0"
    androidTestImplementation "androidx.work:work-testing:$work_version"
}

def props = new Properties()

if (project.file("key.properties").exists()) {
    props.load(new FileInputStream(project.file("key.properties")))
} else {
    logger.warn('Using sample keystore!!')
    props.load(new FileInputStream(project.file("key.properties.sample")))
}

android.signingConfigs.release.storeFile rootProject.file(props.keyStore)

android.signingConfigs.release.storePassword props.keyStorePassword

android.signingConfigs.release.keyAlias props.keyAlias

android.signingConfigs.release.keyPassword props.keyAliasPassword
